# Find a compatible Qt version
# Inputs: WITH_SERVER, WITH_CLIENT, WITH_ORACLE, WITH_DBCONVERTER, FORCE_USE_QT5
# Output: COCKATRICE_QT_VERSION_NAME -- Example values: Qt5, Qt6
# Output: SERVATRICE_QT_MODULES
# Output: COCKATRICE_QT_MODULES
# Output: ORACLE_QT_MODULES
# Output: DBCONVERTER_QT_MODULES

set(REQUIRED_QT_COMPONENTS Core)
if(WITH_SERVER)
    set(_SERVATRICE_NEEDED Network Sql WebSockets)
endif()
if(WITH_CLIENT)
    set(_COCKATRICE_NEEDED Concurrent Gui Multimedia Network PrintSupport Svg Widgets WebSockets)
endif()
if(WITH_ORACLE)
    set(_ORACLE_NEEDED Concurrent Network Svg Widgets)
endif()
if(WITH_DBCONVERTER)
    set(_DBCONVERTER_NEEDED Network Widgets)
endif()

set(REQUIRED_QT_COMPONENTS
        ${REQUIRED_QT_COMPONENTS}
        ${_SERVATRICE_NEEDED}
        ${_COCKATRICE_NEEDED}
        ${_ORACLE_NEEDED}
        ${_DBCONVERTER_NEEDED})
list(REMOVE_DUPLICATES REQUIRED_QT_COMPONENTS)

if(NOT FORCE_USE_QT5)
    # Core5Compat is Qt6 Only
    find_package(Qt6 6.2.3 COMPONENTS Core5Compat ${REQUIRED_QT_COMPONENTS} QUIET HINTS ${Qt6_DIR})
endif()
if(Qt6_FOUND)
    set(COCKATRICE_QT_VERSION_NAME Qt6)
else()
    find_package(Qt5 5.8.0 COMPONENTS ${REQUIRED_QT_COMPONENTS} QUIET HINTS ${Qt5_DIR})
    if(Qt5_FOUND)
        set(COCKATRICE_QT_VERSION_NAME Qt5)
    else()
        message(FATAL_ERROR "No suitable version of Qt was found")
    endif()
endif()

if(Qt5_POSITION_INDEPENDENT_CODE OR Qt6_FOUND)
    set(CMAKE_POSITION_INDEPENDENT_CODE ON)
endif()

# guess plugins and libraries directory
set(QT_PLUGINS_DIR "${${COCKATRICE_QT_VERSION_NAME}Core_DIR}/../../../plugins")
get_target_property(QT_LIBRARY_DIR ${COCKATRICE_QT_VERSION_NAME}::Core LOCATION)
get_filename_component(QT_LIBRARY_DIR ${QT_LIBRARY_DIR} PATH)

message(STATUS "Found Qt ${${COCKATRICE_QT_VERSION_NAME}_VERSION} at: ${${COCKATRICE_QT_VERSION_NAME}_DIR}")

# Establish imports used by other parts of the code base
string(REGEX REPLACE "([^;]+)" "${COCKATRICE_QT_VERSION_NAME}::\\1" SERVATRICE_QT_MODULES "${_SERVATRICE_NEEDED}")
string(REGEX REPLACE "([^;]+)" "${COCKATRICE_QT_VERSION_NAME}::\\1" COCKATRICE_QT_MODULES "${_COCKATRICE_NEEDED}")
string(REGEX REPLACE "([^;]+)" "${COCKATRICE_QT_VERSION_NAME}::\\1" ORACLE_QT_MODULES "${_ORACLE_NEEDED}")
string(REGEX REPLACE "([^;]+)" "${COCKATRICE_QT_VERSION_NAME}::\\1" DB_CONVERTER_QT_MODULES "${_DBCONVERTER_NEEDED}")
if(Qt6_FOUND)
    list(APPEND SERVATRICE_QT_MODULES ${COCKATRICE_QT_VERSION_NAME}::Core5Compat)
    list(APPEND COCKATRICE_QT_MODULES ${COCKATRICE_QT_VERSION_NAME}::Core5Compat)
    LIST(APPEND ORACLE_QT_MODULES ${COCKATRICE_QT_VERSION_NAME}::Core5Compat)
endif()